buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'docker'


jar {
    baseName = 'wishlist'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework:spring-context'
    compile 'net.sf.ehcache:ehcache:2.9.0'
    compile 'javax.mail:mail:1.4.1'
    compile 'javax.activation:activation:1.1'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'junit:junit'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

bootRun {
    jvmArgs = ["-Dapplication.name=${project.name}",
               "-Dproject.name=${project.name}",
               "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005",
               "-Xmx512m",
               "-Xms128M",
               "-XX:+UseG1GC"
    ]
}

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    applicationName = jar.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from jar
            into 'src/main/docker'
        }
    }
}

